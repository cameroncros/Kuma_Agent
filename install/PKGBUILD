pkgname="kuma_agent"
pkgver="1.1"
pkgrel="1"
pkgdesc="Kuma Agent - Push PC status to Uptime Kuma"
arch=("x86_64" "x86" "arm" "arm64")
licence=("GPL3")
source=("git+https://github.com/cameroncros/kuma_agent.git#tag=v$pkgver")
sha512sums=('SKIP')
makedepends=(cargo)
options=(!debug !lto)

prepare() {
    cd kuma_agent
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd kuma_agent
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd kuma_agent
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd kuma_agent
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
    install -Dm0755 -t "$pkgdir/usr/lib/systemd/system/" "install/kuma_agent.service"
}
